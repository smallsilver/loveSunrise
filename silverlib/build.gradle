apply plugin: 'com.android.library'

apply plugin: 'com.jfrog.bintray'

apply plugin: 'com.github.dcendents.android-maven'

ext {
  PUBLISH_GROUP_ID = 'com.silver'
  PUBLISH_ARTIFACT_ID = 'slib'
  PUBLISH_VERSION = '1.0.1'
}


version project.ext.PUBLISH_VERSION

group = project.ext.PUBLISH_GROUP_ID // Maven Group ID for the artifact，一般填你唯一的包名

android {

//  resourcePrefix "si_" //随意填写

  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 23
    versionCode 10
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  testCompile 'junit:junit:4.12'

  // okhttp
  compile 'com.squareup.okhttp:okhttp:2.7.5'

  compile 'io.reactivex:rxjava:1.1.6'

  compile 'io.reactivex:rxandroid:1.2.1'

  compile 'com.google.code.gson:gson:2.2.4'

  compile("com.crashlytics.sdk.android:crashlytics:2.6.5@aar") {
    transitive = true;
  }

}

// Git仓库的url
def siteUrl = 'https://github.com/smallsilver/SilverLib'
// 项目的主页
def gitUrl = 'git@github.com:smallsilver/SilverLib.git'
// 项目issue
def gitUrlissue = 'https://github.com/smallsilver/SilverLib/issues'

install {

  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'aar'
        // Add your description here
        name 'Android Silverlib' //项目的描述 你可以多写一点
        url siteUrl
        // Set your license
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id 'smallsilver' //填写的一些基本信息
            name 'dongen_wang'
            email 'silversilver123@gmail.com'
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}
task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")
  configurations = ['archives']
  pkg {
    repo = "maven"
    name = "slib" //发布到JCenter上的项目名字
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = ["Apache-2.0"]
    publish = true
  }
}

//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'